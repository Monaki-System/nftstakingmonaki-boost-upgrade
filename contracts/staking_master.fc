#include "imports/stdlib.fc";
#include "jetton/jetton-utils.fc";
#include "constants.fc";

global cell data::items;
global slice data::jetton_master;
global cell data::jetton_wallet_code;
global cell data::helper_code;

global int context::op;
global int context::query_id;
global slice context::sender;

() load_data() impure {
    slice ds = get_data().begin_parse();
    data::items = ds~load_dict();
    data::jetton_master = ds~load_msg_addr();
    data::jetton_wallet_code = ds~load_ref();
    data::helper_code = ds~load_ref();
}

() save_data() impure {
    set_data(begin_cell()
        .store_dict(data::items)
        .store_slice(data::jetton_master)
        .store_ref(data::jetton_wallet_code)
        .store_ref(data::helper_code)
    .end_cell());
}

(slice, int) dict_get?(cell dict, int key_len, slice index) asm(index dict key_len) "DICTGET" "NULLSWAPIFNOT";

(cell, slice) get_stateinit_and_address_of_helper(slice item) inline {
    cell stateinit = begin_cell()
        .store_uint(0, 2)
        .store_dict(data::helper_code)
        .store_dict(begin_cell()
            .store_slice(my_address())
            .store_slice(item)
            .store_uint(0, 74)
        .end_cell())
        .store_uint(0, 1)
    .end_cell();
    slice address = begin_cell()
        .store_uint(4, 3)
        .store_int(0, 8)
        .store_uint(cell_hash(stateinit), 256)
    .end_cell().begin_parse();
    return (stateinit, address);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_bits() < 96) {
        return ();
    }

    context::op = in_msg_body~load_uint(32);
    context::query_id = in_msg_body~load_uint(64);
    (_, context::sender) = in_msg_full.begin_parse().skip_bits(4).load_msg_addr();

    load_data();

    if (context::op == op::nft::ownership_assigned) {
        slice prev_owner = in_msg_body~load_msg_addr();
        slice forward_payload = in_msg_body;
        if (forward_payload~load_int(1)) {
            forward_payload = forward_payload~load_ref().begin_parse();
        }

        int stake? = 0;
        int option = 0;
        if (forward_payload.slice_bits() == 40) {
            int op = forward_payload~load_uint(32);
            (_, int f?) = data::items.dict_get?(267, context::sender);
            option = forward_payload~load_uint(8);
            stake? = (op == op::stake) & f? & ((option == 7) | (option == 14) | (option == 30));
        }
        
        if (stake?) {
            (cell stateinit, slice address) = get_stateinit_and_address_of_helper(context::sender);
            send_raw_message(begin_cell()
                .store_uint(0x18, 6)
                .store_slice(address)
                .store_coins(0)
                .store_uint(6, 108)
                .store_ref(stateinit)
                .store_uint(op::process_stake, 32)
                .store_uint(context::query_id, 64)
                .store_slice(prev_owner)
                .store_uint(option, 8)
            .end_cell(), 64);
        } else {
            send_raw_message(begin_cell()
                .store_uint(0x18, 6)
                .store_slice(context::sender)
                .store_coins(0)
                .store_uint(0, 107)
                .store_uint(op::nft::transfer, 32)
                .store_uint(context::query_id, 64)
                .store_slice(prev_owner)
                .store_uint(0, 8)
            .end_cell(), 64);
        }
    }

    elseif (context::op == op::request_claim) {
        slice item = in_msg_body~load_msg_addr();
        slice staker = in_msg_body~load_msg_addr();
        int option = in_msg_body~load_uint(8);

        (_, slice address) = get_stateinit_and_address_of_helper(item);

        throw_unless(error::wrong_sender, equal_slices(context::sender, address));

        slice jetton_wallet_address = calculate_user_jetton_wallet_address(my_address(), data::jetton_master, data::jetton_wallet_code);

        (slice value, int f?) = data::items.dict_get?(267, item);
        int daily_reward = value~load_coins();
        int amount = daily_reward * option;

        send_raw_message(begin_cell()
            .store_uint(0x18, 6)
            .store_slice(jetton_wallet_address)
            .store_coins(const::transfer_fees)
            .store_uint(0, 107)
            .store_uint(op::jetton::transfer, 32)
            .store_uint(context::query_id, 64)
            .store_coins(amount)
            .store_slice(staker)
            .store_uint(0, 8)
        .end_cell(), 1);

        send_raw_message(begin_cell()
            .store_uint(0x18, 6)
            .store_slice(item)
            .store_coins(const::transfer_fees)
            .store_uint(0, 107)
            .store_uint(op::nft::transfer, 32)
            .store_uint(context::query_id, 64)
            .store_slice(staker)
            .store_uint(0, 8)
        .end_cell(), 1);
    }

    else {
        throw(0xffff);
    }

    save_data();
}

slice get_helper_address(slice item) method_id {
    load_data();
    (_, slice address) = get_stateinit_and_address_of_helper(item);
    return address;
}
